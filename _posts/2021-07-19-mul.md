---
title: mul
categories: [longxin_nscscc]
comments: true
---
>>龙芯杯开源代码：<http://cpu.csc-he.com/a/shiyanshebei/2019/0902/59.html>

[“龙芯杯”全国大学生计算机系统能力培养大赛——信息汇总](https://github.com/loongson-education/nscscc-wiki)

# 电路级实现乘法器

>语言基础：verilog

>使用软件：vivado 2019.2

>用途：用于nscscc乘法器的优化部分

首先附上孟学长的博客：

[八位右移位乘法器](https://www.cnblogs.com/mxdon/p/13959134.html)

[八位“Booth二位乘算法”乘法器](https://www.cnblogs.com/mxdon/p/13970070.html)

目录：

[八位左移位乘法器](##八位左移位乘法器)

[八位右移位乘法器](##八位右移位乘法器)

[八位“Booth二位乘算法”乘法器](##八位“Booth二位乘算法”乘法器)

## 八位左移位乘法器

简单说，类似小学的乘法算术题，每一次乘法之后，把对应部分积（被乘数 或者 0）**左移**相应的位数，再相加，得到最终结果

![2021-07-19-mul1.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul1.png)

## 八位右移位乘法器

>>虚假的右移位

用下面两张图片来解释一下所谓的“虚假”：

![2021-07-19-mul2.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul2.png)

![2021-07-19-mul3.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul3.png)

>>真正的右移位

真正的右移位，其实是针对**小数**计算而言的

我们计算**整数乘法**的时候，习惯性的左移，根本原因是我们习惯了整数的小数点在最右侧，只要不超过那个小数点，就可以直接相加得结果。

而**小数计算**，因为小数点在左边的缘故，在计算机当中，就可以根据小数点位置不变的思想，来将部分积右移，从而实现一个右移乘法器。这种思想所成的乘法器，也称为**定点乘法器**。

计算时，每次得到的部分积，都会与上一次得到的部分积相加，而每次相加结果的最后一位不参与运算，可直接右移转存，则部分积占用的空间显然比左移运算小多了。一半通过这样的方式转存，而剩下的一半直接作为高位。

通过下面这个栗子说明：

![2021-07-19-mul4.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul4.png)

![2021-07-19-mul5.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul5.png)

>实战：八位右移位乘法器的具体实现

>>代码一：

右移位乘法器的逻辑很简单，每次根据乘数位是否为<font color=FF0000> 0 </font>进行判断，如果是<font color=FF0000> 0 </font>，则<font color=FF0000> 部分积=前一步的部分积+0 </font>，如果是<font color=FF0000> 1 </font>，则<font color=FF0000> 部分积=前一步的部分积+被乘数 </font>，同时每次将部分积的最低位存储至结果的低位中，最后一次的部分积则为结果的高位。

```verilog
module mul_8_1(
    res,mul1,mul2
    );
    
    input wire[7:0] mul1;
    input wire[7:0] mul2;
    output reg[15:0] res;
    
    reg [7:0] A; //部分积
    integer i;
    reg [7:0] temp_x; //乘法运算时，被乘数与乘数最后一位的积的最后一位不参与最后的加法运算，将其右移另存。
    always @(*) begin
        A=0;
        for ( i=0 ; i<8 ; i=i+1 ) begin
            if (mul2[i]) begin
                A=A+{1'b0,mul1};//部分积移位前可能会比乘数多一位
            end else begin
                A=A+9'b000000000;
            end
            temp_x[i]=A[0];
            A=A>>1;
        end
        res={A,temp_x};
    end
endmodule
```

在RTL级电路综合中很少使用for循环语句。主要原因就是for循环会被综合器展开为所有变量情况的执行语句，每个变量独立占用寄存器资源，每条执行语句并不能有效地复用硬件逻辑资源，造成巨大的资源浪费。也就是说，for语句循环几次，就是将相同的电路复制几次。循环次数越多，综合电路占用面积越大，综合就越慢。

所以我们稍微改改写法，就可以解决for循环了。

>>代码二：

所幸八位的乘法器也并不算复杂，可以写一个并行计算的部分积。这样也就解决了for循环的问题。
```verilog
`define size 8
module mul_8_1(
    input wire[`size-1:0] mul1,
    input wire[`size-1:0] mul2,
    output wire[2*`size-1:0] res
    );
    wire [`size:0] A[`size-1:0];
    wire [`size-1:0] low_pro;
    
    //分步将每次的for循环结果计算出来
    assign A[0]=mul2[0] ? {1'b0,mul1} : 9'd0 ;
    assign A[1]=mul2[1] ? ((A[0]>>1)+{1'b0,mul1}) : ((A[0]>>1)+9'd0) ;
    assign A[2]=mul2[2] ? ((A[1]>>1)+{1'b0,mul1}) : ((A[1]>>1)+9'd0) ;
    assign A[3]=mul2[3] ? ((A[2]>>1)+{1'b0,mul1}) : ((A[2]>>1)+9'd0) ;
    assign A[4]=mul2[4] ? ((A[3]>>1)+{1'b0,mul1}) : ((A[3]>>1)+9'd0) ;
    assign A[5]=mul2[5] ? ((A[4]>>1)+{1'b0,mul1}) : ((A[4]>>1)+9'd0) ;
    assign A[6]=mul2[6] ? ((A[5]>>1)+{1'b0,mul1}) : ((A[5]>>1)+9'd0) ;
    assign A[7]=mul2[7] ? ((A[6]>>1)+{1'b0,mul1}) : ((A[6]>>1)+9'd0) ;
    
    //每次将部分积的最低位存储至结果的低位中
    assign low_pro[0] = A[0][0];
    assign low_pro[1] = A[1][0];
    assign low_pro[2] = A[2][0];
    assign low_pro[3] = A[3][0];
    assign low_pro[4] = A[4][0];
    assign low_pro[5] = A[5][0];
    assign low_pro[6] = A[6][0];
    assign low_pro[7] = A[7][0];

    //最后一次的部分积则为结果的高位
    assign res = {(A[7]>>1),low_pro};
endmodule
```

>>测试文件：

由于两个都是乘法器，输入和输出端口都一样，则可以用同样的测试文件进行测试：

```verilog
module mul_tb(

    );
    reg [7:0] mul1,mul2;
    wire [15:0] res;
    reg clk;
    initial begin
        mul1<=8'd7;
        mul2<=8'd3;
        clk<=0;
    end

    always #10 clk=~clk;
    always @(posedge clk) begin
        mul2<=mul2+1'b1;
    end

    mul_8_1 try(.mul1(mul1),.mul2(mul2),.res(res));
endmodule
```
>>仿真波形图

![2021-07-19-mul6.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul6.png)

![2021-07-19-mul7.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul7.png)

## 八位“Booth二位乘算法”乘法器

>>原理

### 补码乘法器

补码的加法：

$X_{补}+Y_{补}=[X+Y]_{补}$

补码的乘法：

$X*Y_{补}=[X*Y]_{补}=X_{补}*Y$

$[X*Y]_{补}=X_{补}*Y_{1补}-y_{7}*X_{补}*2^7$

其中，$Y_{1补}=y_{6}y_{6}y_{5}y_{4}y_{3}y_{2}y_{1}y_{0}$

$Y_{补}=y_{7}y_{6}y_{5}y_{4}y_{3}y_{2}y_{1}y_{0}$

### Booth一位乘

$Y=-y_{7}*2^7+y_{6}*2^6+y_{5}*2^5+y_{4}*2^4+y_{3}*2^3+y_{2}*2^2+y_{1}*2^1+y_{0}*2^0$

$Y=-y_{7}*2^7+(2-1)y_{6}*2^6+(2-1)y_{5}*2^5+(2-1)y_{4}*2^4+(2-1)y_{3}*2^3+(2-1)y_{2}*2^2+(2-1)y_{1}*2^1+(2-1)y_{0}*2^0$

$Y=-y_{7}*2^7+(y_{6}*2^7-y_{6}*2^6)+(y_{5}*2^6-y_{5}*2^5)+(y_{4}*2^5-y_{4}*2^4)+......+(y_{0}*2^1-y_{0}*2^0)$

$Y=(y_{6}-y_{7})*2^7+(y_{5}-y_{6})*2^6+(y_{4}-y_{5})*2^5+......+(y_{-1}-y_{0})*2^0$

其中，$y_{-1}=0$

![2021-07-19-mul8.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul8.png)

再举个例子来计算，以(-6)x(-7)为例，补码乘是1010x1001，列出竖式：

![2021-07-19-mul9.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul9.png)

这就是Booth一位乘算法的原理。其优点就在于不用再像补码乘法器那样，不需要专门对最后一次部分积采用补码减法。

由于每次判断两位数字，增大了电路的复杂度，那么为什么booth乘法器如此好用呢？

其实booth一位乘算法并不常用，但是booth二位乘就不一样了，通过增加一定的空间复杂度，将运算周期减为一半！

### Booth二位乘

$Y=-y_{7}*2^7+y_{6}*2^6+y_{5}*2^5+y_{4}*2^4+y_{3}*2^3+y_{2}*2^2+y_{1}*2^1+y_{0}*2^0$

$Y=-2*y_{7}*2^6+y_{6}*2^6+(y_{5}*2^6-2*y_{5}*2^4)+y_{4}*2^4+(y_{3}*2^4-2*y_{3}*2^2)+y_{2}*2^2+(y_{1}*2^2-2*y_{1}*2^0)+y_{0}*2^0+y_{-1}*2^0$

$Y=(y_{5}+y_{6}-2*y_{7})*2^6+(y_{3}+y_{4}-2*y_{5})*2^4+(y_{1}+y_{2}-2*y_{3})*2^2+(y_{-1}+y_{0}-2*y_{1})*2^0$

![2021-07-19-mul10.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul10.png)

再举个例子来计算，仍以(-6)x(-7)为例，补码乘是1010x1001，列出竖式：

![2021-07-19-mul11.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul11.png)

运算周期减半了！

好了，那Booth乘法器有没有三位乘呢？可以有，但是三位的时候就会出现加 $3*X_{补}$，$2*X_{补}$ 可以通过左移一位得到，而  $3*X_{补}$ 就有点麻烦了，所以不再介绍，至于四位乘、八位乘，想挑战的同学可以挑战一下。

>实战：八位“Booth二位乘算法”乘法器的具体实现

>>代码

这是一个八位Booth二位乘算法的乘法器
```verilog
`define size 8
module mul_8_1(
    input wire [`size - 1 : 0] mul1,mul2,
    input clk,
    input wire [2:0] clk_cnt,//运算节拍，相当于状态机了，8位的话每次运算有4个拍
    output wire [2*`size - 1 : 0] res
    );

    //由于传值默认就是补码，所以只需要再计算“负补码”即可
    wire [`size - 1 : 0] bmul1,bmul2;
    assign bmul1 = (~mul1 + 1'b1) ;
    assign bmul2 = (~mul2 + 1'b1) ;//其实乘数2的负补码也没用到。
	//其实可以把状态机的开始和结束状态都写出来，我懒得写了，同学们可以尝试一下啊~
    parameter   zeroone       =   3'b000,
                twothree      =   3'b001,
                fourfive      =   3'b010,
                sixseven      =   3'b011;
    //y(i-1),y(i),y(i+1)三个数的判断寄存器，由于有多种情况，也可以看成状态机（也可以改写成状态机形式，大家自己试试吧）
    reg [2:0] temp;

    //部分积
    reg [2*`size-1 : 0] A;
	//每个节拍下把相应位置的数据传给temp寄存器
    always @ (posedge clk) begin
        case(clk_cnt)
            zeroone  : temp <= {mul2[1:0],1'b0};
            twothree : temp <= mul2[3:1];
            fourfive : temp <= mul2[5:3];
            sixseven : temp <= mul2[7:5];
            default : temp <= 0;
        endcase
    end
	
    always @(posedge clk) begin
        if (clk_cnt == 3'b100) begin//如果节拍到4就让部分积归0，此时已经完成一次计算了
            A <= 0;
        end else case (temp)
            3'b000,3'b111 :   begin//这些是从高位到低位的判断，别看反了噢
                A <= A + 0;
            end
            3'b001,3'b010 : begin//加法操作使用补码即可，倍数利用左移解决 //加mul1的补码
                A <= A + ({{8{mul1[`size-1]}},mul1} << 2*(clk_cnt-1));
            end
            3'b011 : begin//加2×mul1的补码
                A <= A + ({{8{mul1[`size-1]}},mul1} << 2*(clk_cnt-1) + 1);
            end
            3'b100: begin//减法操作利用“负补码”改成加法操作，倍数利用左移解决 //减2×mul1的补码
                A <= A + ({{8{bmul1[`size-1]}},bmul1} << 2*(clk_cnt-1) + 1);
            end
            3'b101,3'b110 : begin//减mul1的补码
                A <= A + ({{8{bmul1[`size-1]}},bmul1} << 2*(clk_cnt-1));
            end
            default: A <= 0;
        endcase
    end
	//当节拍到4的时候写入结果寄存器。
    assign res = (clk_cnt == 3'b100) ? A : 0;
endmodule
```

>>测试文件

```verilog
module mul_tb(

    );
    
    reg [7:0] mul1,mul2;
    wire [15:0] res;
    reg clk;

    wire clk_en;
    reg [2:0] clk_cnt;

    initial begin
        mul1 <= -8'd7;
        mul2 <= -8'd3;
        clk <= 0;
        clk_cnt <= 3'b0;
    end

    always # 10 clk = ~clk;
	//clk_cnt发生器，懒人版
    always @(posedge clk) begin
        clk_cnt <= clk_cnt + 1'b1;
        if (clk_cnt == 3'b100)
            clk_cnt <= 3'b00;
    end
	//每次运算结束后，让乘数变化，以便产生不同的数据用以观察
    assign clk_en = (clk_cnt == 3'b100) ? 1'b1 : 1'b0;
    always @ (posedge clk_en) begin
        mul2 <= mul2 + 1'b1;
    end


    mul_8_1 try(.mul1(mul1),.mul2(mul2),.res(res),.clk(clk),.clk_cnt(clk_cnt));
endmodule
```

>>仿真波形图

![2021-07-19-mul12.png](https://gitee.com/jie_xue_du/learn_images_ben/raw/master/images/beibei_knowledge/2021-07-19-mul12.png)
